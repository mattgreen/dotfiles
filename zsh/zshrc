#zmodload zsh/zprof

typeset -U path

# Favor Homebrew binaries over system
path=(/usr/local/bin /usr/local/sbin $path)

# Add external brew commands
path=($HOME/.dotfiles/osx/brew $path)

# Aliases
alias ls='ls -F'
alias ll='ls -lha'
alias ..='cd ..'
alias ...='cd ../..'
alias reload!='. ~/.zshrc'
alias benchdotfilesloadtime='for i in $(seq 1 10); do /usr/bin/time zsh -i -c exit; done'

alias gs='git status -s'

# History config
HISTFILE=~/.zsh_history
HISTSIZE=4000
SAVEHIST=4000

setopt APPEND_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS # Drop extraneous whitespace

# Terminal config
export CLICOLOR=1
export EDITOR="mvim -f"
export TERM=xterm-256color

# Color completions properly
export LS_COLORS="di=34;40:ln=35;40:so=32;40:pi=33;40:ex=31;40:bd=34;46:cd=34;43:su=0;41:sg=0;46:tw=0;42:ow=0;43:"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# Case-insensitive matching
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'

# Pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# Disable directory completion removing slashes
setopt no_auto_remove_slash

# Consider / a word separator
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

# Use emacs mode; modal CLI editing is weird
bindkey -e
bindkey "^[[1;9C" forward-word
bindkey "^[[1;9D" backward-word
bindkey "^U" backward-kill-line
bindkey "^X\\x7f" backward-kill-line

# Jump support
function jump() {
    cd ~/Projects/$1
}

function _jump() {
    _directories -W ~/Projects
}

alias j='jump'

# Set terminal/window titles
precmd() {
    local tabtitle="$(basename "$PWD")"

    echo -ne "\e]1;${tabtitle: -24}\a"
    echo -ne "\e]2;$PWD\a"
}

preexec() {
    echo -ne "\e]1;$2\a"
    echo -ne "\e]2;$PWD\a"
}

# Load completions
fpath=($HOME/.dotfiles/zsh/completions $fpath)

autoload -U compinit
compinit

compdef _jump jump

for file in $HOME/.dotfiles/zsh/*.zsh
do
    source $file
done

#zprof
